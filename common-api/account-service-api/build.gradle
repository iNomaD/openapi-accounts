apply plugin: "org.openapi.generator"
apply plugin: "java-library"

dependencies {
    api project(":common-api")

    implementation "org.springframework.cloud:spring-cloud-starter-openfeign"
    implementation "org.hibernate.validator:hibernate-validator"
    implementation "io.springfox:springfox-swagger2"
    implementation "org.openapitools:jackson-databind-nullable"
}

openApiGenerate {
    generatorName = "spring"
    library = "spring-cloud"
    inputSpec = "${specificationDir}/accounts-spec.yaml"
    outputDir = "${buildDir}/generated/open-api"
    apiPackage = "com.devexperts.accounts.api"
    modelPackage = "com.devexperts.accounts.model"
    modelNameSuffix = "Json"
    systemProperties = [
            apis: "",
            models: ""
    ]
    importMappings = [
            "Error": "com.devexperts.common.model.ErrorJson"
    ]
}

idea.project.settings {
    taskTriggers {
        beforeBuild tasks.getByName("openApiGenerate")
        beforeRebuild tasks.getByName("openApiGenerate")
    }
}

sourceSets {
    main {
        java {
            srcDir "${buildDir.absolutePath}/generated/open-api/src/main/java"
        }
    }
}

compileJava {
    dependsOn  tasks.openApiGenerate
    source "${buildDir}/generated/open-api/src/main/java"
}
