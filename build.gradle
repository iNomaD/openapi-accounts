plugins {
    id "org.jetbrains.gradle.plugin.idea-ext" version "0.5"
    id "org.springframework.boot" version "2.2.7.RELEASE" apply false
    id "io.spring.dependency-management" version "1.0.9.RELEASE" apply false
    id "org.openapi.generator" version "4.3.1" apply false
}

allprojects {
    apply plugin: "java"
    apply plugin: "io.spring.dependency-management"

    group = 'com.devexperts'
    version = '0.0.1-SNAPSHOT'
    sourceCompatibility = '11'

    repositories {
        mavenCentral()
    }

    test {
        useJUnitPlatform()
    }

    ext {
        springBootVersion = "2.2.7.RELEASE"
        springCloudVersion = "Hoxton.SR4"
        h2dbVersion = "1.4.200"
        lombokVersion = "1.18.12"
        springfoxSwaggerVersion = "2.9.2"
        hibernateValidatorVersion = "6.0.17.Final"
        jacksonDatabindNullableVersion = "0.2.1"
        specificationDir = "${rootDir}/common-api/src/main/resources/api"
    }

    dependencyManagement {
        imports {
            mavenBom "org.springframework.boot:spring-boot-dependencies:${springBootVersion}"
            mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
        }
        dependencies {
            dependencySet(group: "org.springframework.boot", version: "${springBootVersion}") {
                entry("spring-boot-starter-actuator")
                entry("spring-boot-starter-data-jpa")
                entry("spring-boot-starter-web")
                entry("spring-boot-devtools")
            }
            dependency("org.springframework.boot:spring-boot-starter-test:${springBootVersion}") {
                exclude group: "junit", name: "junit"
            }
            dependency "com.h2database:h2:${h2dbVersion}"
            dependency "org.projectlombok:lombok:${lombokVersion}"
            dependency "io.springfox:springfox-swagger2:${springfoxSwaggerVersion}"
            dependency "io.springfox:springfox-swagger-ui:${springfoxSwaggerVersion}"
            dependency "org.openapitools:jackson-databind-nullable:${jacksonDatabindNullableVersion}"
            dependency "org.hibernate:hibernate-validator:${hibernateValidatorVersion}"
        }
    }
}