plugins {
    id 'org.springframework.boot' version '2.2.2.RELEASE'
    id 'io.spring.dependency-management' version '1.0.8.RELEASE'
    id 'org.openapi.generator' version '4.2.2'
    id 'org.jetbrains.gradle.plugin.idea-ext' version '0.5'
    id 'java'
}

group = 'com.devexperts'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '11'

configurations {
    developmentOnly
    runtimeClasspath {
        extendsFrom developmentOnly
    }
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-actuator'
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    developmentOnly 'org.springframework.boot:spring-boot-devtools'

    runtimeOnly 'com.h2database:h2'

    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'

    implementation 'io.springfox:springfox-swagger2:2.9.2'
    implementation 'org.springdoc:springdoc-openapi-ui:1.2.21'
    implementation 'org.openapitools:jackson-databind-nullable:0.2.1'

    testImplementation('org.springframework.boot:spring-boot-starter-test') {
        exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
    }
}

test {
    useJUnitPlatform()
}

openApiGenerate {
    generatorName = "spring"
    inputSpec = "${projectDir}/src/main/resources/api/accounts-spec.yaml"
    outputDir = "${buildDir}/generated/open-api"
    apiPackage = "com.devexperts.accounts.api"
    invokerPackage = "com.devexperts.accounts.handler"
    modelPackage = "com.devexperts.accounts.model"
    modelNameSuffix = "Json"
    configOptions = [
            delegatePattern: "true"
    ]
}

idea.project.settings {
    taskTriggers {
        beforeBuild tasks.getByName("openApiGenerate")
        beforeRebuild tasks.getByName("openApiGenerate")
        afterSync tasks.getByName("openApiGenerate")
        beforeSync tasks.getByName("openApiGenerate")
    }
}

sourceSets {
    main {
        java {
            srcDir "${buildDir.absolutePath}/generated/open-api/src/main/java"
        }
    }
}

compileJava {
    dependsOn  tasks.openApiGenerate
    source "${buildDir}/generated/open-api/src/main/java"
}

springBoot {
    mainClassName = "com.devexperts.accounts.AccountsApp"
}
